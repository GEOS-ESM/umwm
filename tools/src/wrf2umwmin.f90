program wrf2umwmin

  ! Takes a WRF file name as a command line argument, and 
  ! outputs a UMWM input file based on data in the WRF file. Supported
  ! WRF files are input (wrfinput*), output (wrfout*) or restart 
  ! (wrfrst*). Conversion of fields goes as:
  !
  !     WRF            UMWM        ACTION
  !-----------------------------------------------------------------------
  !    XLONG        -> lon     - Longitude, copied
  !    XLAT         -> lat     - Latitude,  copied
  !    LANDMASK     -> seamask - Seamask
  !    U10          -> uw      - 10-m wind, x-component, copied
  !    V10          -> vw      - 10-m wind, y-component, copied 
  !    PSFC, T2, Q2 -> rhoa    - Moist air density, calculated from
  !                              surface pressure, temperature and 
  !                              specific humidity
  ! 
  ! Note: Water density (rhow) and surface current fields (uc,vc)
  ! are not available from WRF files.

  use netcdf
  use umwm_utils, only: nc_check

  implicit none

  integer :: idm, jdm
  integer :: stat
  integer :: ncid, xdimid, ydimid
  integer :: varid, lonid, latid, maskid, uwid, vwid, rhoaid

  character(9999) :: wrfoutFile
  character(29) :: umwminFile
  character(19) :: time
  character(NF90_MAX_NAME) :: dimname

  real, dimension(:,:), allocatable :: lon, lat, u10, v10, t2, q2, psfc, rhoa

  integer, allocatable :: mask(:,:)

  real, parameter :: Rd = 286.9
  real, parameter :: Rw = 461.5

  ! 1) Get command line argument:
  call get_command_argument(1, value=wrfoutFile, status=stat)
  if (stat /= 0) error stop 'wrf2umwmin: Error: Cannot retrieve argument'

  time = wrfoutFile(len_trim(wrfoutFile)-18:)

  umwminFile = 'umwmin_' // time // '.nc'

  ! 2) Read input file:
  call nc_check(nf90_open(trim(wrfoutFile),nf90_nowrite, ncid))
  call nc_check(nf90_inq_dimid(ncid, 'west_east', xdimid))
  call nc_check(nf90_inq_dimid(ncid, 'south_north', ydimid))
  call nc_check(nf90_inquire_dimension(ncid, xdimid, dimname, idm))
  call nc_check(nf90_inquire_dimension(ncid, ydimid, dimname, jdm))

  allocate(lon(idm,jdm), lat(idm,jdm), mask(idm,jdm), u10(idm,jdm), v10(idm,jdm))
  allocate(psfc(idm,jdm), t2(idm,jdm), q2(idm,jdm), rhoa(idm,jdm))

  call nc_check(nf90_inq_varid(ncid, 'XLONG', varid))
  call nc_check(nf90_get_var(ncid, varid, lon, start=[1,1,1],count=[idm,jdm,1]))

  call nc_check(nf90_inq_varid(ncid, 'XLAT', varid))
  call nc_check(nf90_get_var(ncid, varid, lat, start=[1,1,1], count=[idm,jdm,1]))

  call nc_check(nf90_inq_varid(ncid, 'LANDMASK', varid))
  call nc_check(nf90_get_var(ncid, varid, mask, start=[1,1,1], count=[idm,jdm,1]))

  call nc_check(nf90_inq_varid(ncid, 'U10', varid))
  call nc_check(nf90_get_var(ncid, varid, u10, start=[1,1,1], count=[idm,jdm,1]))

  call nc_check(nf90_inq_varid(ncid, 'V10', varid))
  call nc_check(nf90_get_var(ncid, varid, v10, start=[1,1,1], count=[idm,jdm,1]))

  call nc_check(nf90_inq_varid(ncid, 'PSFC', varid))
  call nc_check(nf90_get_var(ncid, varid, psfc, start=[1,1,1], count=[idm,jdm,1]))

  call nc_check(nf90_inq_varid(ncid, 'T2', varid))
  call nc_check(nf90_get_var(ncid, varid, t2, start=[1,1,1], count=[idm,jdm,1]))

  call nc_check(nf90_inq_varid(ncid, 'Q2', varid))
  call nc_check(nf90_get_var(ncid, varid, q2, start=[1,1,1], count=[idm,jdm,1]))

  call nc_check(nf90_close(ncid))

  ! Compute air density at the surface:
  rhoa = psfc*(1+q2)/(t2*(Rd+Rw*q2))

  ! 3) Generate new umwm.grid file:
  call nc_check(nf90_create(umwminFile, nf90_clobber, ncid))
  call nc_check(nf90_def_dim(ncid, 'x', idm, xdimid))
  call nc_check(nf90_def_dim(ncid, 'y', jdm, ydimid))
  call nc_check(nf90_def_var(ncid, 'lon', nf90_float, [xdimid,ydimid], lonid))
  call nc_check(nf90_def_var(ncid, 'lat', nf90_float, [xdimid,ydimid], latid))
  call nc_check(nf90_def_var(ncid, 'seamask', nf90_int, [xdimid,ydimid], maskid))
  call nc_check(nf90_def_var(ncid, 'uw', nf90_float, [xdimid,ydimid], uwid))
  call nc_check(nf90_def_var(ncid, 'vw', nf90_float, [xdimid,ydimid], vwid))
  call nc_check(nf90_def_var(ncid, 'rhoa', nf90_float, [xdimid,ydimid], rhoaid))
  call nc_check(nf90_put_att(ncid, NF90_GLOBAL, name='history', values='Generated by wrfout2umwmin'))
  call nc_check(nf90_enddef(ncid))
  call nc_check(nf90_put_var(ncid, lonid, lon))
  call nc_check(nf90_put_var(ncid, latid, lat))
  call nc_check(nf90_put_var(ncid, maskid, 1-mask))
  call nc_check(nf90_put_var(ncid, uwid, u10))
  call nc_check(nf90_put_var(ncid, vwid, v10))
  call nc_check(nf90_put_var(ncid, rhoaid, rhoa))
  call nc_check(nf90_close(ncid))

end program wrf2umwmin
